package com.sample

import com.siit.sbnz.timdarmar.models.classes.Employer;
import com.siit.sbnz.timdarmar.models.classes.WorkExperience;

import com.siit.sbnz.timdarmar.models.events.EmployerWorkExperienceEvent;

import com.siit.sbnz.timdarmar.models.enums.EmployerCarelessnessType;
import com.siit.sbnz.timdarmar.models.enums.EmployerRecklessnessType;

import java.time.LocalDate;
import java.time.LocalDateTime;
import java.time.YearMonth;
import java.time.Instant;
import java.time.format.DateTimeFormatter;
import java.util.TimeZone;

declare CarelessEvent
	@role(event)
	employer: Employer
end

declare RecklessEvent
	@role(event)
	employer: Employer
end

declare PunishmentEvent
	@role(event)
	employer: Employer
end

declare PotentialBan
	@role(event)
	employer: Employer
	reason: String
end

declare CounterForAvgMark
	val: double
	inc: int
end

rule "Calculating avg rating from marked working experiences from employer"
	agenda-group "cep"
	when
		$ew: EmployerWorkExperienceEvent($e: employer, $we: WorkExperience,
		$we.dateTo != null, $we.employeeRating != null, $we.employerRating != null, 
		$we.paid == true)
		$counter: CounterForAvgMark(inc >= 2)
		from accumulate
		(
			$ew2: EmployerWorkExperienceEvent 
			(
				this != $ew,
				$ew2.getEmployer().getId() == $e.getId(),
				$ew2.getWorkExperience().getId() != $we.getId(),
				$ew2.getWorkExperience().getDateTo() != null,
				$ew2.getWorkExperience().getEmployeeRating() != null,
				$ew2.getWorkExperience().getEmployerRating() != null,
				$ew2.getWorkExperience().getPaid() == true,
				this meets[24h] $ew
			),
			init
			(
				CounterForAvgMark c = new CounterForAvgMark();
				c.setVal(0.0);
				c.setInc(0);
			),
			action
			(
				c.setVal(c.getVal() + $ew2.getWorkExperience().getEmployerRating());
				c.setInc(c.getInc() + 1);
			),
			result
			(
				c
			)
		)
		not (PotentialBan(employer == $e, reason == "Low avg"))
	then
		System.out.println("Calculating avg rating from marked working experiences from employer");
		double val = $counter.getVal() + $we.getEmployerRating();
		int inc = $counter.getInc() + 1;
		double avg = val/inc;
		String indikator =  "Low avg";
		if (avg <= 6)
			insert(new PotentialBan($e, indikator));
end

rule "Catching umarked working experiences from employer"
	agenda-group "cep"
	when
		$ew: EmployerWorkExperienceEvent($e: employer, $we: WorkExperience, 
		$we.dateTo != null, $we.employeeRating == null, $we.paid == true)
		$value: Number(intValue >= 2) 
		from accumulate 
		(
			$ew2: EmployerWorkExperienceEvent 
			(
				this != $ew,
				$ew2.getEmployer().getId() == $e.getId(),
				$ew2.getWorkExperience().getId() != $we.getId(),
				$ew2.getWorkExperience().getDateTo() != null,
				$ew2.getWorkExperience().getEmployeeRating() == null,
				$ew2.getWorkExperience().getPaid() == true,
				this meets[24h] $ew
			),
			count($ew2)
		)
		not (CarelessEvent(employer == $e)) 
	then
		System.out.println("Catching umarked working experiences from employer");
		int value = $value.intValue() + 1;
		if (value >= 3)
			insert(new CarelessEvent($e));
end

declare CounterForEvent
	points: int
	cnt: int
end

rule "Catching unpaid working experiences from employer"
	agenda-group "cep"
	when
		$ew: EmployerWorkExperienceEvent($e: employer, $we: WorkExperience,
		$we.dateTo != null, $we.paid == false)
		$counter: CounterForEvent(cnt >= 2) 
		from accumulate 
		(
			$ew2: EmployerWorkExperienceEvent 
			(
				this != $ew,
				$ew2.getEmployer().getId() == $e.getId(),
				$ew2.getWorkExperience().getId() != $we.getId(),
				$ew2.getWorkExperience().getDateTo() != null,
				$ew2.getWorkExperience().getPaid() == false,
				this meets[24h] $ew
			),
			init
			(
				CounterForEvent c = new CounterForEvent();
				c.setPoints(1);
				c.setCnt(1);
			),
			action
			(
				LocalDateTime tempTime =
                LocalDateTime.ofInstant(Instant.ofEpochMilli
                ($ew2.getWorkExperience().getDateTo()),
                        TimeZone.getDefault().toZoneId());
                        
                if (YearMonth.now().atDay(1).minusDays(1).isBefore(tempTime.toLocalDate()) && 
                YearMonth.now().atDay(1).plusDays(5).isAfter(tempTime.toLocalDate()))
                	c.setPoints(c.getPoints() + 1);
                else if (YearMonth.now().atDay(1).plusDays(5).isEqual(tempTime.toLocalDate())
                 || YearMonth.now().atDay(1).plusDays(5).isBefore(tempTime.toLocalDate()))
                 	c.setPoints(c.getPoints() + 2);
                c.setCnt(c.getCnt() + 1); 
			),
			result
			(
				c
			)
		)
		not (RecklessEvent(employer == $e))
	then
		System.out.println("Catching unpaid working experiences from employer");
		if ($counter.getPoints() >= 4)
			insert(new RecklessEvent($e));
end

rule "inserting PunishmentEvent"
	agenda-group "cep"
	when
		CarelessEvent($e:employer)
		RecklessEvent($e1:employer)
		eval($e.getId() == $e1.getId())
		not (PunishmentEvent(employer == $e))
	then
		insert(new PunishmentEvent($e));		
end

rule "Catching a punishment event"
	agenda-group "cep"
	when
		PunishmentEvent($e:employer)
		not (PotentialBan(employer == $e, reason == "Punishment"))
	then
		System.out.println("Id: " + $e.getId() + " reason: punishment. ");
		
		$e.setEmployerRecklessnessType(EmployerRecklessnessType.RECKLESS);
        $e.setEmployerCarelessnessType(EmployerCarelessnessType.CARELESS); 
      
		update($e);
		
		insert(new PotentialBan($e, "Punishment"));
end

rule "Ban scenario"
	agenda-group "cep"
	when
		PotentialBan($e:employer, reason == "Punishment")
		PotentialBan($e1:employer, reason == "Low avg")
		eval($e.getId() == $e1.getId())
	then		
		System.out.println("Id: " + $e.getId() + " is banned");
		
		modify($e) { setPenalty(true); }
end
